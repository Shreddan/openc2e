include(BundleUtilities)
set(install_prefix "${CMAKE_INSTALL_PREFIX}")
get_filename_component(install_prefix "${install_prefix}" ABSOLUTE)

message(STATUS "Fixing up bundle: ${install_prefix}")
if(WIN32)
    fixup_bundle("${install_prefix}/openc2e.exe" "" "${CMAKE_CURRENT_BINARY_DIR};${EXTRA_LIB_DIRS}")
elseif(APPLE)
    get_bundle_keys("${install_prefix}/openc2e" "" "${CMAKE_CURRENT_BINARY_DIR};${EXTRA_LIB_DIRS}" keys)
    foreach(k IN LISTS keys)
        if (${k}_COPYFLAG EQUAL 1)
            if (${k}_ITEM MATCHES ".framework")
                get_filename_component(basename "${${k}_RESOLVED_EMBEDDED_ITEM}" NAME)
                string(REPLACE "../Frameworks/" "" ${k}_EMBEDDED_ITEM ${${k}_EMBEDDED_ITEM})
                string(REPLACE "@executable_path/" "" install_location ${${k}_EMBEDDED_ITEM})
                copy_resolved_framework_into_bundle("${${k}_RESOLVED_ITEM}" "${install_prefix}/${install_location}")
            else()
                string(REPLACE "../Frameworks/" "" ${k}_EMBEDDED_ITEM ${${k}_EMBEDDED_ITEM})
                copy_resolved_item_into_bundle("${${k}_RESOLVED_ITEM}" "${install_prefix}")
            endif()
        endif()
    endforeach()
    foreach(k IN LISTS keys)
        if (${k}_COPYFLAG EQUAL 2)
            get_filename_component(original "${${k}_RESOLVED_ITEM}" NAME)
            get_filename_component(basename "${${k}_RESOLVED_EMBEDDED_ITEM}" NAME)
            string(REPLACE "../Frameworks/" "" ${k}_EMBEDDED_ITEM ${${k}_EMBEDDED_ITEM})
            file(CREATE_LINK "${original}" "${install_prefix}/${basename}" SYMBOLIC)
        endif()
    endforeach()
    foreach(k IN LISTS keys)
        if (${k}_COPYFLAG EQUAL 0)
            fixup_bundle_item("${${k}_ITEM}" "" "")
        endif()
    endforeach()
endif()
